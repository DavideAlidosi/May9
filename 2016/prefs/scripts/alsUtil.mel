/// file alsUtil.mel

global proc als_addColorAttr(string $node, string $attrName)
{
    if (objExists($node+"."+$attrName)) return;

    addAttr -ln $attrName -at float3 -usedAsColor -k 1 $node;
    addAttr -ln  ($attrName+"_r") -at "float" -k 1 -p $attrName $node;
    addAttr -ln  ($attrName+"_g") -at "float" -k 1 -p $attrName $node;
    addAttr -ln  ($attrName+"_b") -at "float" -k 1 -p $attrName $node;
}

global proc als_setColorAttr(string $node, string $attrName, float $r, float $g, float$b)
{
	string $attr = ($node+"."+$attrName);
	setAttr ($attr+"_r") $r;
	setAttr ($attr+"_g") $g;
	setAttr ($attr+"_b") $b;
}

global proc int als_isArnoldGeometry(string $node)
{
	string $t = `nodeType $node`;

	return ($t == "mesh");
}

global proc als_setMtoaColor(string $node, string $attr, float $r, float $g, float $b)
{
	string $attrName = ("mtoa_constant_" + $attr);
	als_addColorAttr($node, $attrName);
	als_setColorAttr($node, $attrName, $r, $g, $b);
}

global proc als_setId(string $node, int $id, float $r, float $g, float $b)
{
	string $attrName = ("mtoa_constant_id_" + $id);
	als_addColorAttr($node, $attrName);
	als_setColorAttr($node, $attrName, $r, $g, $b);
}

global proc als_setMtoaColorOnSelection(string $attr, float $r, float $g, float $b)
{
	string $sel[] = `ls -sl`;
	string $desc[] = `listRelatives -ad $sel`;

	for ($d in $desc)
	{
		if (als_isArnoldGeometry($d))
		{
			als_setMtoaColor($d, $attr, $r, $g, $b);
		}
	}
}

global proc als_setIdOnSelection(int $id, float $r, float $g, float $b)
{
	string $sel[] = `ls -sl`;
	string $desc[] = `listRelatives -ad $sel`;

	for ($d in $desc)
	{
		if (als_isArnoldGeometry($d))
		{
			als_setId($d, $id, $r, $g, $b);
		}
	}
}

global proc int als_isArnoldLight(string $node)
{
    string $t = `nodeType $node`;
    return  ($t=="pointLight") || 
            ($t == "spotLight") || 
            ($t == "directionalLight") || 
            ($t == "areaLight") ||
            ($t == "aiAreaLight") ||
            ($t == "aiSkyDomeLight") ||
            ($t == "aiPhotometricLight");
}

global proc als_setLightGroup(string $node, int $group)
{	
	if (!objExists($node+".mtoa_constant_lightGroup"))
	{
		addAttr -ln "mtoa_constant_lightGroup" -at "long" $node;
	}

	setAttr ($node+".mtoa_constant_lightGroup") $group;
}

global proc als_setLightGroupOnSelection(int $group)
{
	string $sel[] = `ls -sl`;
	string $desc[] = `listRelatives -ad $sel`;

	for ($d in $desc)
	{
		if (als_isArnoldLight($d))
		{
			als_setLightGroup($d, $group);
		}
	}
}